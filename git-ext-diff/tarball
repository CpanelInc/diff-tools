#!/usr/local/cpanel/3rdparty/bin/perl

# Copyright 2020, cPanel, L.L.C.
# All rights reserved.
# http://cpanel.net
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
# this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright notice,
# this list of conditions and the following disclaimer in the documentation
# and/or other materials provided with the distribution.
#
# 3. Neither the name of the owner nor the names of its contributors may be
# used to endorse or promote products derived from this software without
# specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
# ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
# FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
# SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
# CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
# OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

use v5.14;    # For s///r
use strict;
use warnings;

BEGIN {
    # Disable support for compressed tarballs, as they make it much less likely
    # for Git to think similar tarballs are renames of one another, which
    # prevents this tool from running in the first place.  The compression also
    # makes it impossible to search for contents within the file using -S/-G.
    #
    # In general, storing compressed files in Git is a bad idea as it actually
    # makes Git's compression much worse -- it often severely hampers delta
    # compression, paradoxically making the repository much larger.  Thus, this
    # limitation also encourages good repository design.
    local $ENV{PERL5_AT_NO_ZLIB} = 1;
    local $ENV{PERL5_AT_NO_BZIP} = 1;
    local $ENV{PERL5_AT_NO_XZ}   = 1;
    use Archive::Tar ();
}
use File::Spec              ();
use Getopt::Long            ();
use Git::Raw::Note          ();
use Git::Raw::Repository    ();
use Git::Raw::Tree::Builder ();

exit _main(@ARGV) unless caller;

sub _main {
    my @args = @_;

    my %conf = (
        overview               => 1,
        cache_ref              => 'refs/notes/cache/tarball',
        full_path              => 1,
        propagate_diff_options => 1,
    );
    Getopt::Long::Parser->new(
        config => [ 'posix_default', 'gnu_compat' ],
    )->getoptionsfromarray(
        \@args,
        'help|?' => \my $print_help,

        'overview!'   => \$conf{overview},
        'cache!'      => \$conf{cache},
        'cache-ref=s' => sub { $conf{cache} = 1; $conf{cache_ref} = "$_[1]" },

        'indent:s'           => \$conf{indent},
        'full-path!'         => \$conf{full_path},
        'munge-components=i' => \$conf{munge_components},

        'propagate-diff-options!' => \$conf{propagate_diff_options},
        'exclude-diff-option=s@'  => \$conf{exclude_diff_option},
        'prepend-diff-option=s@'  => \$conf{prepend_diff_option},
        'append-diff-option=s@'   => \$conf{append_diff_option},

        'show-content!' => \my $show_content,
        'show-header:s' => \my $show_header,
    );

    if ($print_help) {
        print_help();
        return 0;
    }

    if ( defined $show_header ) {
        $conf{header_format} = $show_header || 'attr-simple';
        $conf{header_file}   = '.header';
        $conf{content_file}  = '-' if !defined $show_content || $show_content;
    }
    elsif ( !defined $show_content || $show_content ) {
        $conf{content_file} = '';
    }

    # Performs exec system call, preventing it from returning.
    _execute_diff_for_git( \%conf, @args );
}

# Takes a configuration hash, followed by all of the arguments that Git passes
# to the external diff tool.  Note that the annotations are not officially
# documented and sometimes are not provided when one would expect to see them.
sub _execute_diff_for_git {
    my ( $conf, $old_git_path, $old_file, $old_hex, $old_mode, $new_file, $new_hex, $new_mode, $new_git_path, $git_annotations ) = @_;
    $new_git_path //= $old_git_path;

    my $repo = Git::Raw::Repository->discover('.');

    # Get a tree object that corresponds to the given tarball's contents
    # (subject to formatting options in config).
    my $old_tree = _extract_files_to_tree( $conf, $repo, $old_hex, $old_file );
    my $new_tree = _extract_files_to_tree( $conf, $repo, $new_hex, $new_file );
    if ( $conf->{full_path} ) {

        # Prepend the path of the tarball to the contents of the tree to create
        # a new tree with the combined paths.  This allows the same blob to be
        # used at multiple paths in the repository.
        $old_tree = build_full_tree( $repo, $old_git_path, $old_tree );
        $new_tree = build_full_tree( $repo, $new_git_path, $new_tree );
    }
    else {
        # When a full path is not used, we force the indent option to true (if
        # it was not already set).
        $conf->{indent} //= '';
    }

    # Parse diff options from parent git binary.  In particular, we need to
    # un-reverse the arguments when -R is passed because taking them as is
    # would mean that the path prefixes (e.g. `a/` & `b/`) would be in the
    # wrong order.
    my @options = get_git_diff_options($conf);
    my %parsed  = parse_git_diff_options(@options);
    ( $old_tree, $new_tree ) = ( $new_tree, $old_tree ) if $parsed{reverse};

    if ( $conf->{overview} ) {
        my $src_prefix = ( map { m/^--src-prefix=(.*)|^--no-prefix()/ ? ($1) : () } @options )[-1] // "a/";
        my $dst_prefix = ( map { m/^--dst-prefix=(.*)|^--no-prefix()/ ? ($1) : () } @options )[-1] // "b/";
        ( $src_prefix, $dst_prefix ) = ( $dst_prefix, $src_prefix ) if $parsed{reverse};

        # Check the last color-related command line option or git-config.
        my $no_color;
        if ( my $last_color_option = ( grep { m/^--(?:no-)?color/ } @options )[-1] ) {
            $no_color = $last_color_option =~ m/--no-color|--color=never/;
        }
        else {
            $no_color = system qw(git config --get-colorbool color.diff);    # Will check for TTY
        }
        my $header_color = $no_color ? '' : qx{git config --get-color color.diff.meta bold};

        # If git annotations are provided, any line prefix will already be
        # prepended to all of the lines.  We remove that line prefix and then
        # re-prepend it to ensure the color formatting doesn't modify the color
        # of the line prefix itself, as git-diff does not apply coloring to the
        # line prefix, which can have graphable (i.e. isgraph()) characters.
        my $prefix = $parsed{line_prefix} // '';
        print_header( "diff --git $src_prefix$old_git_path $dst_prefix$new_git_path", $header_color, $prefix );
        print_header( $git_annotations =~ s/^\Q$prefix\E//mgr,                        $header_color, $prefix ) if $git_annotations;

        # The "diff --git" header never prints /dev/null; everything else does.
        my $src = $old_hex eq '.' ? '/dev/null' : "$src_prefix$old_git_path";
        my $dst = $new_hex eq '.' ? '/dev/null' : "$dst_prefix$new_git_path";
        print "${prefix}Tarballs $src and $dst differ\n";
    }

    amend_git_diff_options( $conf, \@options, \%parsed );
    exec 'git', 'diff', $old_tree, $new_tree, @options;
    die "Cannot exec: $!";
}

# Prints custom header (or meta) lines with the proper formatting.
sub print_header {
    my ( $data, $color, $prefix ) = @_;

    # Handle each line separately so the formatting affects only the text and
    # does not run to the end of the line (e.g. if the background color is set)
    # and so the line prefix is applied to every line.
    foreach ( split m/\n/, $data ) {
        print $prefix if length $prefix;
        print $color  if $color;
        print $_;

        # Clear the formatting, but only if formatting was applied.  Piping the
        # ANSI codes to another program - even if it has no effect on the
        # terminal - could break the receiving tool's parsing of the contents.
        print "\e[m" if $color;
        print "\n";
    }

    return;
}

# Creates a reproducible identifier for the format that does not contain \012.
sub identify_cache_format {
    my ($conf) = @_;

    # Any arbitrary, non empty string can be used as the format identifier.  To
    # make adding new components to the identifier easier, we define a
    # `key=value` format, separated by spaces.  Both key and value may contain
    # spaces and no escaping mechanism is defined since we don't need to parse
    # the identifier - we are only comparing it to see if it is a verbatim
    # match.
    #
    # For keys defining blob contents, the value is the name of the file in
    # which those blobs are stored (under the tree defined by the actual
    # filename in the tarball).
    #
    # When the file name is a blob, the value `=<format>` is used.
    my @format;
    push @format, "munge=$conf->{munge_components}" if $conf->{munge_components};
    push @format, sprintf 'header=%s,%s', $conf->{header_format}, $conf->{header_file} =~ s/\012//gr if length $conf->{header_file};
    if ( length $conf->{content_file} ) {
        push @format, sprintf 'file=%s', $conf->{content_file} =~ s/\012//gr;
    }
    elsif ( defined $conf->{content_file} ) {
        die "content_file must not be the empty string" if length $conf->{header_file};
        push @format, '=file';
    }

    return join ' ', @format;
}

# Prepend the given path to the existing tree to create a new tree.
sub build_full_tree {
    my ( $repo, $path, $tree ) = @_;

    foreach my $dir ( reverse File::Spec::Unix->splitdir($path) ) {
        my $builder = Git::Raw::Tree::Builder->new($repo);
        $builder->insert( $dir, $tree, 040000 );
        $tree = $builder->write();
    }

    return $tree;
}

# Take a tar file (a blob) and return the tree that represents the extracted
# contents of that blob according to the formatting options specified in the
# configuration.  When extracting, the tree is stored in the repository that
# houses the original blob; it will be reaped in a future garbage collection.
sub _extract_files_to_tree {
    my ( $conf, $repo, $id, $tar_file ) = @_;
    my $cache_ref = $conf->{cache} ? $conf->{cache_ref} : undef;
    my $format    = identify_cache_format($conf);

    # Check for a cached tree object in the repository and re-use it if it is
    # valid.  This involves checking for a cache entry in the notes and, if one
    # is found, verifying the tree object still exists (i.e. that it was not
    # pruned during garbage collection).  Cached trees let this tool avoid
    # re-extracting the archive contents, which can save a lot of time with
    # large archives, and can help with repeated runs or when a blob appears
    # multiple times, as it would be with "git log -p --ext-diff".
    my ( $message, $indexed );
    if ( $id ne '0' x 40 && $id ne '.' && $cache_ref ) {
        my $note = Git::Raw::Note->read( $repo, $id, $cache_ref );
        $message = $note ? $note->message() : '';
        foreach my $line ( split m/\012/, $message ) {
            my ( $sha1, $form ) = split m/ /, $line, 2;
            if ( $form eq $format ) {
                my $tree = $repo->lookup($sha1);
                return $tree if defined $tree;
                $indexed = $sha1;
                last;
            }
        }
    }

    # Get a clean index, in memory; modifications to this object will not alter
    # the Git repository because we never write the index to disk.
    my $index = $repo->index();
    $index->clear();

    # Add files to index so the index looks like the tarball's contents
    # (subject to formatting options specified in $conf).
    my $iter = Archive::Tar->iter($tar_file);
    while ( my $f = $iter->() ) {
        _add_file_to_index( $conf, $index, $f );
    }

    # Create root tree.  This writes objects to the repository's object store,
    # but does not change the repository state (vis-à-vis git-status).
    my $tree = $index->write_tree();

    # Sanity check to make sure nothing changed.  If an issue is encountered,
    # it likely means the other cached indices have mismatches as well, so all
    # must be addressed.  Die with an error and force the user to take care of
    # it.  We intentionally do not delete the ref for the user, as that could
    # cause two different runs of this tool to produce different results (e.g.
    # during a git-log, the first commit could use stale data while a later one
    # got the cache refreshed).
    if ( $indexed && $tree ne $indexed ) {
        die "Cached tree does not match tarball.\nClear all cached trees with 'git update-ref -d $cache_ref'\n";
    }

    # Add tree information to note for caching if not already stored.  Do not
    # store uncommitted tarballs, as their contents are subject to change.
    if ( defined $message && !$indexed ) {
        $message .= "$tree $format\012";
        Git::Raw::Note->create( $repo, $id, $message, $cache_ref, 1 );
    }

    return $tree;
}

# Once published, these functions should not change to ensure cache indices do
# not become invalid.  They should remain "constant".
use constant HEADER_FORMAT => {
    'raw'         => sub { return $_[0]->raw },
    'attr-simple' => sub {
        my ($file) = @_;
        my $header = '';
        $header .= sprintf "Mode:     %04o\n",  $file->mode;
        $header .= sprintf "User:     %d/%s\n", $file->uid, $file->uname;
        $header .= sprintf "Group:    %d/%s\n", $file->gid, $file->gname;
        $header .= sprintf "Modified: %s\n",    scalar localtime( $file->mtime );

        # Types '0' and '2' can be inferred from the file mode in Git.  If type
        # is '1', then we show the Hardlink field, so we don't need to show the
        # Type field.  Symlinks already show linkname in the content file ('-'),
        # making the field unnecessary.
        $header .= sprintf "Type:     %s\n", $file->type if $file->type ne '0' && $file->type ne '1' && $file->type ne '2';
        $header .= sprintf "Hardlink: %s\n", $file->linkname if $file->type eq '1';

        # This will probably never happen, but account for it in an attempt to future-proof this format.
        $header .= sprintf "Device:   %d, %d\n", $file->devmajor, $file->devminor if $file->devmajor || $file->devminor;
        return $header;
    },
    'attr-all' => sub {
        my ($file) = @_;
        my $header = '';
        $header .= sprintf "Name:         %s\n",   $file->name;
        $header .= sprintf "Mode:         %04o\n", $file->mode;
        $header .= sprintf "UID:          %d\n",   $file->uid;
        $header .= sprintf "GID:          %d\n",   $file->gid;
        $header .= sprintf "Size:         %d\n",   $file->size;
        $header .= sprintf "Mtime:        %s\n",   $file->mtime;
        $header .= sprintf "Checksum:     %s\n",   $file->chksum;
        $header .= sprintf "Type:         %s\n",   $file->type;
        $header .= sprintf "Linkname:     %s\n",   $file->linkname;
        $header .= sprintf "Magic:        %s\n",   $file->magic;
        $header .= sprintf "Version:      %s\n",   $file->version;
        $header .= sprintf "Device Major: %d\n",   $file->devmajor;
        $header .= sprintf "Device Minor: %d\n",   $file->devminor;
        $header .= sprintf "User Name:    %s\n",   $file->uname;
        $header .= sprintf "Group Name:   %s\n",   $file->gname;
        $header .= sprintf "Prefix:       %s\n",   $file->prefix;
        return $header;
    },

    # TODO: attr-format:<format-string>, xattr-*?
};

# Copy an individual file from the archive into the (disposable) index
# according to the format specified in the configuration.
sub _add_file_to_index {
    my ( $conf, $index, $file ) = @_;

    my $full_path = $file->full_path;
    if ( $conf->{munge_components} ) {
        my @dirs = File::Spec::Unix->splitdir($full_path);

        # Take the smaller the path component length (less one) and the given argument.
        my $munge_count = $conf->{munge_components} < (@dirs - 1) ? $conf->{munge_components} : (@dirs - 1);

        $dirs[$_] = '*' for ( 0 .. $munge_count - 1 );
        $full_path = File::Spec::Unix->catfile(@dirs);
    }

    if ( length $conf->{header_file} ) {
        my $header;
        if ( 'CODE' eq ref &HEADER_FORMAT->{ $conf->{header_format} } ) {
            $header = &HEADER_FORMAT->{ $conf->{header_format} }->($file);
        }
        else {
            die "Invalid header format: $conf->{header_format}\n";
        }

        my $name = File::Spec::Unix->catfile( $full_path, $conf->{header_file} );
        $index->add_frombuffer( $name, \$header, 0100644 );
    }

    if ( defined $conf->{content_file} ) {
        $full_path .= "/$conf->{content_file}" if length $conf->{content_file};

        if ( $file->type eq '0' ) {    # Normal file
            my $mode = $file->mode & 0100 ? 0100755 : 0100644;
            $index->add_frombuffer( $full_path, $file->get_content_by_ref, $mode );
        }
        elsif ( $file->type eq '1' ) {    # Hardlinked file (to previous entry in tarball)
            my $entry = $index->find( $file->linkname ) // die "Cannot find entry: $file->linkname";
            $index->add( $entry->clone($full_path) );
        }
        elsif ( $file->type eq '2' ) {    # Symlink
            $index->add_frombuffer( $full_path, $file->linkname, 0120000 );
        }
        elsif ( $file->type ne '5' ) {    # Not a directory (e.g. socket, pipe, device).
            die "Unrecognized type: " . $file->type;
        }
    }

    return;
}

# Get list of command line arguments to process on a system with procfs.
sub get_arguments_from_proc {
    my ($pid) = @_;
    my $file = "/proc/$pid/cmdline";

    local $/;
    open my $fh, '<', $file or die "Cannot open “$file”: $!";
    my @args = split m/\0/, <$fh>;
    close $fh or warn "Cannot close “$file”: $!";

    return @args;
}

# The git-diff options we will propagate.
#
# See https://git-scm.com/docs/diff-options for list of available options.
use constant { NO_VALUE => 0, OPTIONAL_VALUE => 1, REQUIRE_VALUE => 2 };
use constant DIFF_SHORT_OPTS => {
    p => &NO_VALUE,         # Implied
    u => &NO_VALUE,         # Implied
    s => undef,             # This tool would not run if this option is given [NO_VALUE]
    U => &REQUIRE_VALUE,
    X => &OPTIONAL_VALUE,
    z => &NO_VALUE,
    B => &OPTIONAL_VALUE,
    M => &OPTIONAL_VALUE,
    C => &OPTIONAL_VALUE,
    D => &NO_VALUE,
    l => &REQUIRE_VALUE,
    S => &REQUIRE_VALUE,    # Parent must select us for search
    G => &REQUIRE_VALUE,    # Parent must select us for search
    O => &REQUIRE_VALUE,
    R => &NO_VALUE,
    a => &NO_VALUE,
    b => &NO_VALUE,
    w => &NO_VALUE,
    W => &NO_VALUE,
};
use constant DIFF_LONG_OPTS => {
    '--patch'    => &NO_VALUE,         # Implied
    '--no-patch' => undef,             # This tool would not be run if this option is given [NO_VALUE]
    '--unified'  => &OPTIONAL_VALUE,
    '--output'   => undef,             # This will lead to processes overwriting each other; ignore for now [REQUIRE_VALUE]

    # These inject raw output into the middle of a patch, so they are only
    # allowed when --indent is given.
    '--raw'            => &NO_VALUE,
    '--patch-with-raw' => &NO_VALUE,

    '--output-indicator-new'     => &REQUIRE_VALUE,
    '--output-indicator-old'     => &REQUIRE_VALUE,
    '--output-indicator-context' => &REQUIRE_VALUE,

    '--indent-heuristic'    => &NO_VALUE,
    '--no-indent-heuristic' => &NO_VALUE,

    '--minimal'        => &NO_VALUE,
    '--patience'       => &NO_VALUE,
    '--histogram'      => &NO_VALUE,
    '--anchored'       => &REQUIRE_VALUE,
    '--diff-algorithm' => &REQUIRE_VALUE,

    # These inject stat output into the middle of a patch, so they are only
    # allowed when --indent is given.
    '--stat'            => &OPTIONAL_VALUE,
    '--stat-width'      => &REQUIRE_VALUE,
    '--stat-name-width' => &REQUIRE_VALUE,
    '--stat-count'      => &REQUIRE_VALUE,
    '--compact-summary' => &NO_VALUE,
    '--numstat'         => &NO_VALUE,
    '--shortstat'       => &NO_VALUE,
    '--dirstat'         => &OPTIONAL_VALUE,
    '--cumulative'      => &NO_VALUE,
    '--dirstat-by-file' => &OPTIONAL_VALUE,
    '--summary'         => &NO_VALUE,
    '--patch-with-stat' => &NO_VALUE,

    '--name-only'   => undef,    # This tool would not run if this option is given [NO_VALUE]
    '--name-status' => undef,    # This tool would not run if this option is given [NO_VALUE]
    '--submodule'   => undef,    # Tarballs cannot contain submodules [OPTIONAL_VALUE]

    '--color'           => &OPTIONAL_VALUE,
    '--no-color'        => &NO_VALUE,
    '--color-moved'     => &OPTIONAL_VALUE,
    '--no-color-moved'  => &NO_VALUE,
    '--color-moved-ws'  => &REQUIRE_VALUE,
    '--word-diff'       => &OPTIONAL_VALUE,
    '--word-diff-regex' => &REQUIRE_VALUE,
    '--color-words'     => &OPTIONAL_VALUE,

    '--no-renames'      => &NO_VALUE,
    '--rename-empty'    => &NO_VALUE,
    '--no-rename-empty' => &NO_VALUE,

    '--check'              => &NO_VALUE,
    '--ws-error-highlight' => &REQUIRE_VALUE,

    '--full-index' => &NO_VALUE,
    '--binary'     => &NO_VALUE,
    '--abbrev'     => &OPTIONAL_VALUE,

    '--break-rewrites'      => &OPTIONAL_VALUE,
    '--find-renames'        => &OPTIONAL_VALUE,
    '--find-copies'         => &OPTIONAL_VALUE,
    '--find-copies-harder'  => &NO_VALUE,
    '--irreversible-delete' => &NO_VALUE,

    '--diff-filter'   => &REQUIRE_VALUE,    # Parent must select us for filter
    '--find-object'   => undef,             # Git will not search via an external diff [REQUIRE_VALUE]
    '--pickaxe-all'   => &NO_VALUE,         # Parent must select us for search
    '--pickaxe-regex' => &NO_VALUE,         # Parent must select us for search

    '--relative'    => undef,               # Handled by parent diff [OPTIONAL_VALUE]
    '--no-relative' => &NO_VALUE,

    '--text'                => &NO_VALUE,
    '--ignore-cr-at-eol'    => &NO_VALUE,
    '--ignore-space-at-eol' => &NO_VALUE,
    '--ignore-space-change' => &NO_VALUE,
    '--ignore-all-space'    => &NO_VALUE,
    '--ignore-blank-lines'  => &NO_VALUE,

    '--inter-hunk-context' => &REQUIRE_VALUE,
    '--function-context'   => &NO_VALUE,

    # Handled by parent diff.  If we did accept this, we would (almost) always
    # exit non-zero, which the parent diff would interpret as an error, causing
    # it to abort.
    '--exit-code' => undef,    # NO_VALUE
    '--quiet'     => undef,    # NO_VALUE

    '--ext-diff'    => &NO_VALUE,    # Implied on git-diff.  Since we use git-diff, nested ext-diff will occur.
    '--no-ext-diff' => undef,        # This tool would not run if this option is given [NO_VALUE]
    '--textconv'    => &NO_VALUE,
    '--no-textconv' => &NO_VALUE,

    '--ignore-submodules' => undef,    # Tarballs cannot contain submodules [OPTIONAL_VALUE]

    '--src-prefix'  => &REQUIRE_VALUE,
    '--dst-prefix'  => &REQUIRE_VALUE,
    '--no-prefix'   => &NO_VALUE,
    '--line-prefix' => &REQUIRE_VALUE,

    '--ita-invisible-in-index' => undef,    # "git add -N" cannot occur [NO_VALUE]
};

# Return all of the allowed short & long options found in the given arguments.
#
# For the allowed options, provide a hashref where the key is the option and
# the value is a constant that represents whether there are no additional
# values for the argument, an optional value, or if a value is required.  With
# short options, the key is only the letter; for long options, it is the full
# option with both dashes prefixed.
#
# For simplicity, bundled short options will be split into individual
# arguments in the returned list.
sub collect_allowed_options {
    my ( $short_opts, $long_opts, $args ) = @_;

    my @options;
    while (@$args) {
        my $arg = shift @$args;
        next if substr( $arg, 0, 1 ) ne '-';
        last if $arg eq '--';

        if ( substr( $arg, 1, 1 ) ne '-' ) {    # If arg is a short option (or short option bundle)
            my $chars = substr $arg, 1;         # Strip leading dash
            while ( length $chars ) {
                my $char = substr $chars, 0, 1;
                if ( $short_opts->{$char} ) {    # If it can have a value
                    push @options, "-$chars";
                    push @options, shift(@$args) if length($chars) == 1 && $short_opts->{$char} == &REQUIRE_VALUE;
                    last;                        # Since a value can follow, this must be the last option
                }
                elsif ( defined $short_opts->{$char} ) {
                    push @options, "-$char";
                }    # Note: the bundled form of `--` (e.g. `-a-`) does not seem to be supported by Git.
                $chars = substr $chars, 1;
            }
        }
        else {
            # Note: We assume _optional_ values must be specified with `=` in
            # the same argument and cannot appear as a following argument.
            my $index = index $arg, '=';
            my $key = $index == -1 ? $arg : substr( $arg, 0, $index );
            if ( defined $long_opts->{$key} ) {
                push @options, $arg;
                push @options, shift(@$args) if $long_opts->{$key} == &REQUIRE_VALUE && $index == -1;
            }
        }
    }

    return @options;
}

# Query the command line of the parent process so we can extract its list of
# diff options.  In doing so, our embedded diff will better match the output
# formatting of its parent, for a more native look.
sub get_git_diff_options {
    my ($conf) = @_;
    return if !$conf->{propagate_diff_options};

    my $diff_short_opts = &DIFF_SHORT_OPTS;
    my $diff_long_opts  = &DIFF_LONG_OPTS;
    if ( $conf->{exclude_diff_option} ) {
        foreach my $exclude ( @{ $conf->{exclude_diff_option} } ) {
            die "Value to --exclude-diff-option= must begin with a dash: $exclude\n" if substr( $exclude, 0, 1 ) ne '-';
            if ( substr( $exclude, 1, 1 ) ne '-' ) {
                my @chars = split m//, substr( $exclude, 1 );
                delete $diff_short_opts->{$_} foreach (@chars);
            }
            else {
                delete $diff_long_opts->{$exclude};
            }
        }
    }

    # Without an indent to separate the display, injected stat and raw
    # information would break the visual flow.
    delete @$diff_long_opts{
        qw(
          --raw
          --patch-with-raw

          --stat
          --stat-width
          --stat-name-width
          --stat-count
          --compact-summary
          --numstat
          --shortstat
          --dirstat
          --cumulative
          --dirstat-by-file
          --summary
          --patch-with-stat
          )
      }
      if !defined $conf->{indent};

    # TODO: Support systems without procfs (e.g. Windows, macOS).
    my @args = get_arguments_from_proc(getppid);
    return collect_allowed_options( $diff_short_opts, $diff_long_opts, \@args );
}

# Inject additional options into git-diff command line based on configuration
# options and values from current command line.
sub amend_git_diff_options {
    my ( $conf, $opts, $values ) = @_;

    if ( $conf->{prepend_diff_option} ) {
        unshift @$opts, @{ $conf->{prepend_diff_option} };
    }
    if ( $conf->{append_diff_option} ) {
        push @$opts, @{ $conf->{append_diff_option} };
    }

    if ( defined $conf->{indent} ) {

        # Prepend the line-prefix from our parent command, if given, to the
        # indent prefix to make the resulting indentation fit in.
        #
        # If no indent value is provided (i.e. the flag is given without value
        # or the value is set to the empty string), then default to TAB (to
        # ensure indents cannot be nullified, which helps with --no-full-path).
        push @$opts, sprintf "--line-prefix=%s%s", $values->{line_prefix} // '', length $conf->{indent} ? $conf->{indent} : "\t";
    }

    return;
}

# Parse select options from the arguments, needed for correctly selecting the
# values to pass to the child diff, without removing them from the argument
# list (so the child diff still receives them).
sub parse_git_diff_options {
    my %values;
    Getopt::Long::Parser->new(
        config => [ 'posix_default', 'gnu_compat', 'permute', 'bundling', 'pass_through' ],
    )->getoptionsfromarray(
        [@_],    # Do not modify the original arguments.
        'R'             => \$values{reverse},
        'line-prefix=s' => \$values{line_prefix},
    );

    return %values;
}

sub print_help {
    print <<HELP;
Setup: git config diff.<driver>.command '$0 [OPTIONS] --'
Compare the archived files within tarballs as part of git-diff.
Do NOT call this tool directly; it is designed to be used by Git.

Options:
    --help, -?      Print this message and exit

    --no-full-path  Do not prepend tarball path to filenames; implies --indent.
    --no-overview   Omit entry showing "Tarballs a/ and b/ differ".
    --cache         Use cache for faster lookup of tarball contents.
    --no-cache      Disable cache use (default).
    --cache-ref=<ref>
                    Specify the git-notes ref used to cache the git tree content
                    for a blob.  Implies --cache.  When not given, --cache uses
                    "refs/notes/cache/tarball".
    --indent[=<prefix>]
                    When displaying the diff of tarball contents, prefix each
                    line with <prefix>.  The overview is not indented.  If not
                    given, TAB is used for <prefix>.  Enables propagation on
                    --raw & --stat, et. al.
    --munge-components=<n>
                    Replace the first <n> path components of the tarballs with
                    "*".  This hides renames of the top-level directories.
    --no-propagate-diff-options
                    Disable propagation of diff options from parent process.
    --exclude-diff-option=<option>
                    Prevent the given option from being propagated to this
                    tool's diff creator.  This can disable unwanted processing,
                    e.g. --exclude-diff-option=--check.  Can be given multiple
                    times.
    --prepend-diff-option=<option>
                    Specify an option to put before the propagated ones; useful
                    for setting default behavior that can be overturned.  Can
                    be given multiple times.
    --append-diff-option=<option>
                    Specify an option to put after the propagated ones. Can be
                    given multiple times.

    --no-show-content
                    Do not show file content in diff.
    --show-header[=<format>]
                    Show tar header for file.  This turns the filename into a
                    tree, which has a '.header' blob that shows the attributes
                    in the header.  If --no-show-content is not given, the file
                    content is shown in a '-' blob in this tree.  Formats:
                    - raw:          Verbatim (binary) representation of header.
                    - attr-all:     Show all header fields as key/value pairs.
                    - attr-simple:  Show fields that cannot be guessed from the
                                    content file and tar(1) defaults -- uncommon
                                    values will cause field to appear. (default)

Diff Option Propagation
    To seamlessly integrate with Git's diff output, this tool reads the diff
    options passed to the Git command (the one that transparently calls this
    tool).  Supported options are re-used in the diff this tool creates.
    See "git help diff" for a list of diff options; not all are propagated.

Once configured, this tool can be called as part of normal Git commands.
Usage: git diff [diff-options]
       git show --ext-diff [show-options]
       git log -p --ext-diff [log-options]
       git <command> -p --ext-diff [command-options]

See also: "Generating diff text" in `man 5 gitattributes`.
HELP
}
